writeln("<html>");
writeln("<head><title>" + GetFileName() + "</title></head>");
writeln("<body>");
writeln("<h1>" + GetFilePath() + GetFileName() + "</h1>");
writeln("<p align=\"right\">");
writeln("  " + GetYear() + "/" + GetMonth() + "/" + GetDate());
writeln("</p>");
writeln("");
writeln("<table border=\"1\">");

for (y = 1; y <= Bottom; y++) {
  writeln("<tr>");
  for (x = 1; x <= Right; x++) {
    if ([x,y] == "") {
      writeln("  <td><br></td>");
    } else if ([x,y].startsWith("http://") ||
               [x,y].startsWith("https://")) {
      writeln("  <td><a href=\"" + [x,y] + "\">"+ [x,y] + "</a></td>");
    } else {
      str = [x,y].replaceAll("&", "&amp;")
                 .replaceAll("<", "&lt;")
                 .replaceAll(">", "&gt;")
                 .replaceAll("\n", "<br>");
      writeln("  <td>" + str + "</td>");
    }
  }
  writeln("</tr>");
}

writeln("</table>");
writeln("");
writeln("</body>");
writeln("</html>");
